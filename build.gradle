plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id "org.owasp.dependencycheck" version "6.5.0.1"
    id 'com.adarshr.test-logger' version '3.1.0'
    id "org.sonarqube" version "3.3"
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.google.guava:guava:31.0.1-jre'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

tasks.sonarqube.dependsOn tasks["jacocoTestReport"]

/* https://github.com/ben-manes/gradle-versions-plugin */
dependencyUpdates {
    rejectVersionIf {
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'pr']
                .any { qualifier -> it.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/ }
    }
    revision = 'release'
}

application {
    mainClass = 'com.akafred.aoc.AoC21Kt'
}
